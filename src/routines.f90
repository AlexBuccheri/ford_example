!> Some collection of routines, for demonstrating documentation 
module routines
  implicit none
  public

contains

  ! NOTE: Standard fortran comments are not parsed by Ford or Doxygen 
  
  ! --------------------------------------
  ! Standard Ford Documentation Style
  ! --------------------------------------
  ! docmark = !!
  ! Documentation is placed beneath the code being documented
  ! i.e. the opposite from Doxygen.    
  ! General Markdown syntax is correctly parsed.    
  ! Latex is treated with MathJax.

  function norm(a) result(norm_of_a)
    !! Compute the norm of the vector \(\mathbf{a}\)
    !! The norm of the vector is defined as:
    !! \[ |\mathbf{a}| = \sqrt{ \mathbf{a} \cdot \mathbf{a} } \]
    
    real(dp), intent(in) :: a(:)
    !! Vector a
    real(dp) :: norm_of_a
    !! norm of vector a
    
    norm_of_a = sqrt(dot_product(a, a))
  end function norm


  
  ! -----------------------------------------------------------------
  !  Ford Documentation Placement Above the Line
  ! -----------------------------------------------------------------
  ! * Available since Ford v1.0.0.
  ! * This is my preferred style, and offers most interoperability with Doxygen 
  !
  ! Documentation is placed above the code that's being documented.
  ! predocmark = !>
  ! This documentation can also be parsed by Doxygen.
  ! One is also able to add documentation after variables, on the same line.
  ! * See the declaration of distance_matrix, for example.

  
  !> Construct distance matrix. 
  !>                                                                                              
  !> Given a set of atomic positions, construct a distance matrix:
  !> 
  !> \[
  !>    d_{ij} = |\mathbf{r}_j - \mathbf{r}_i|,
  !> \]
  !> 
  !> where \(\mathbf{r}_i\) is the position of atom \(i\).
  !>
  !> For more information on distance matrix, one can look at
  !> [scipy](https://docs.scipy.org/doc/scipy/reference/generated/scipy.spatial.distance_matrix.html).  
  !> Markdown hyperlinks work for both Ford and Doxygen.
  !>
  !> Examples of tags:
  !> @note
  !>  This is an example of a note, generated by the note tag
  !> @endnote
  !> 
  !> @todo This is an example of a todo, generated by the todo tag.  
  !> All following text will be included with this tag unless
  !> the tag is closed or a new tag is opened.
  !>
  !> @warning This is an example of a warning, generated by the warning tag
  !> @endwarning
  !> 
  !> Back to normal text. note, todo and warning tags are also recognised
  !> by doxygen, however the closing tags are not
  
  function distance_matrix(positions) result(d)
    !> atomic positions 
    real(dp), intent(in) :: positions(:,:)
    real(dp), allocatable :: d(:,:)            !! distance matrix
    integer :: ia, ja, n_atoms
    
    n_atoms = size(positions, 2)
    allocate(d(n_atoms, n_atoms))
    
    do ia = 1, n_atoms
       do ja = ia, n_atoms
          d(ia, ja) = norm(positions(ja, :) - positions(ia, :))
          d(ja, ia) = d(ia, ja)
       enddo
    enddo
    
  end function distance_matrix


  
  ! -----------------------------------------------------------------
  ! Doxygen Documentation
  ! -----------------------------------------------------------------
  ! Ford does not recognise important Doxygen tags, such as @brief, @param
  ! and @return, however they still get parsed.
  ! 
  ! The start and end of equations also uses slightly different syntax
  ! => equations are not cross-compatible 
  ! Ford: \[ \]     Doxygen  \f[  \f]

  
  !> @brief Compute distance matrix
  !> 
  !> Given a set of atomic positions, construct a distance matrix:
  !> 
  !> \f[
  !>    d_{ij} = |\mathbf{r}_j - \mathbf{r}_i|,
  !> \f]
  !>
  !> where \(\mathbf{r}_i\) is the position of atom \(i\).
  !>
  !> For more information on distance matrix, one can look at
  !> [scipy](https://docs.scipy.org/doc/scipy/reference/generated/scipy.spatial.distance_matrix.html). 
  !> Markdown hyperlinks work for both Ford and Doxygen. 
  !> 
  !> @param[in]  positions  atomic positions
  !> @return    d          distance_matrix
  function distance_matrix_again(positions) result(d)
    real(dp), intent(in) :: positions(:,:)
    real(dp), allocatable :: d(:,:)   
    integer :: ia, ja, n_atoms
    
    n_atoms = size(positions, 2)
    allocate(d(n_atoms, n_atoms))
    
    do ia = 1, n_atoms
       do ja = ia, n_atoms
          d(ia, ja) = norm(positions(ja, :) - positions(ia, :))
          d(ja, ia) = d(ia, ja)
       enddo
    enddo
    
  end function distance_matrix_again

  
end module routines
